// SPDX-License-Identifier: BUSL-1.1
// Copyright (c) 2025 Supra Labs

pragma solidity 0.8.22;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/proxy/utils/Initializable.sol";
import "contracts/token-vault/implementations/Helpers.sol";
import "contracts/interfaces/IWETH.sol";
import "contracts/interfaces/IVault.sol";


/// @title Token Vault Implementation
/// @notice Stores and manages locked ERC-20 tokens with configurable limits and delayed admin withdrawals.
/// @dev Upgradeable via ERC1967 proxy. Follows the `IVault` interface.
contract VaultImplementation is Initializable, Helpers {
    using SafeERC20 for IERC20;

    constructor() {
        _disableInitializers();
    }

    /// @notice Initializes admin, token bridge service and admin withdrawal delay
    function initialize(
        address _admin,
        address _tokenBridgeService,
        uint256 _delay
    ) public initializer {
        _setTokenBridgeService(_tokenBridgeService);
        _setAdmin(_admin);
        _setAdminWithdrawDelay(_delay);
    }
    /// @notice Updates the admin address
    function setAdmin(address _admin) external onlyAdmin {
        _setAdmin(_admin);
        emit IVault.UpdatedAdmin(msg.sender, _admin);
    }

    /// @notice Sets the token bridge contract address
    function setTokenBridgeContract(address _tokenBridgeContract) external onlyAdmin isNotPaused {
        _setTokenBridgeService(_tokenBridgeContract);
    }
    /// @notice Pauses or unpauses the vault
    function changeState(bool _isPaused) external onlyAdmin {
        isPaused = _isPaused;
    }

    /// @notice Locks ERC-20 tokens into the vault
    function lockTokens(
        address token,
        uint256 amount
    ) external onlyBridge isNotPaused {
        uint256 totalLockedAmount = getLockedTokenBalance(token);
        uint256 newTotalLockedAmount = totalLockedAmount + amount;
        (uint256 min, uint256 max, uint256 globalMax) = getLockLimits(token);
        if (!isValidLimit(amount, min, max)) revert LimitBreached();
        if (!isValidGlobalLimit(newTotalLockedAmount, globalMax)) revert GlobaTokenLockLimitBreached();
        lockedTokens[token] = newTotalLockedAmount;
        emit IVault.Locked(token, amount);
    }

    /// @notice Locks native currency by wrapping to the provided WETH-like token and locking it
    function lockNative(
        address nativeToken,
        uint256 amount
    ) external payable onlyBridge isNotPaused {
        if (msg.value != amount) revert InvalidInput();
        uint256 totalLockedAmount = getLockedTokenBalance(nativeToken);
        uint256 newTotalLockedAmount = totalLockedAmount + amount;

        (uint256 min, uint256 max, uint256 globalMax) = getLockLimits(nativeToken);
        if (!isValidLimit(amount, min, max)) revert LimitBreached();
        if (!isValidGlobalLimit(newTotalLockedAmount, globalMax)) revert GlobaTokenLockLimitBreached();
        
        lockedTokens[nativeToken] = newTotalLockedAmount;
        IWETH(nativeToken).deposit{value: amount}();
        emit IVault.Locked(nativeToken, amount);
    }

    /// @notice Releases tokens to a recipient
    function release(
        address token,
        uint256 amount,
        address to
    ) external onlyBridge isNotPaused isReleaseEnabled {
        if (to == address(0) || amount == 0 || token == address(0)) revert InvalidInput();
        (uint256 min, uint256 max) = getReleaseLimits(token);
        if (!isValidLimit(amount, min, max)) revert LimitBreached();

        if (amount > getLockedTokenBalance(token)) revert InsufficientBalance();
        lockedTokens[token] -= amount;
        IERC20(token).safeTransfer(to, amount);
        emit IVault.Released(to, token, amount);
    }

    /// @notice Schedules an admin withdrawal with delay
    function addAdminWithdraw(
        address token,
        uint256 amount,
        address to
    ) external onlyAdmin returns (bytes32) {
        if (to == address(0) || amount == 0 || token == address(0)) revert InvalidInput();
        if (amount > getLockedTokenBalance(token)) revert InsufficientBalance();

        (bytes32 id, uint256 _withdrawNonce) = _addTransfer(token, amount, to);
        emit IVault.AdminWithdrawAdded(id, token, to, amount, _withdrawNonce, block.timestamp);
        return id;
    }

    /// @notice Cancels a previously scheduled admin withdrawal
    function removeAddedAdminWithdraw(bytes32 id) external onlyAdmin returns (bytes32) {
        IVault.DelayedTransfer storage transfer = adminTransfers[id];
        if (!transfer.isAdded || transfer.nonce >= getNextWithdrawNonce()) {
            revert WithdrawDoesNotExist(id);
        }
        transfer.isAdded = false;
        emit IVault.AdminWithdrawRemoved(id, transfer.token, transfer.to, transfer.amount, transfer.nonce, block.timestamp);
        return id;
    }

    /// @notice Executes a scheduled admin withdrawal after the configured delay
    function execAdminWithdraw(bytes32 id) external onlyAdmin {
        IVault.DelayedTransfer memory transfer = adminTransfers[id];
        if (!transfer.isAdded  || transfer.nonce >= getNextWithdrawNonce()) {
            revert WithdrawDoesNotExist(id);
        }

        if (block.timestamp > transfer.timestamp + adminWithdrawDelay) {
            delete adminTransfers[id];

            if (transfer.amount > getLockedTokenBalance(transfer.token)) revert InsufficientBalance();
            lockedTokens[transfer.token] -= transfer.amount;
            
            IERC20(transfer.token).safeTransfer(transfer.to, transfer.amount);
            emit IVault.AdminWithdrawExecuted(
                id,
                transfer.token,
                transfer.to,
                transfer.amount,
                transfer.nonce,
                block.timestamp
            );
        } else {
            revert TryLater();
        }
    }

    /// @notice Sets per-token lock limits
    function setLockLimits(
        address[] calldata tokens,
        uint256[] calldata minLimits,
        uint256[] calldata maxLimits,
        uint256[] calldata globalMaxLimits //  globalMaxLimits == 0 means no limit 
    ) external onlyAdmin {
        if (
            tokens.length != minLimits.length ||
            tokens.length != maxLimits.length ||
            tokens.length != globalMaxLimits.length 
        ) {
            revert InvalidInput();
        }

        for (uint256 i = 0; i < tokens.length; i++) {
            if (tokens[i] == address(0) || (minLimits[i] >= maxLimits[i])) {
                revert InvalidInput();
            }
            lockLimits[tokens[i]] = IVault.LockLimit(minLimits[i], maxLimits[i], globalMaxLimits[i]);
        }
    }

    /// @notice Sets per-token release limits
    function setReleaseLimits(
        address[] calldata tokens,
        uint256[] calldata minLimits,
        uint256[] calldata maxLimits
    ) external onlyAdmin {
        if (
            tokens.length != minLimits.length ||
            tokens.length != maxLimits.length 
        ) {
            revert InvalidInput();
        }

        for (uint256 i = 0; i < tokens.length; i++) {
            if (tokens[i] == address(0) || (minLimits[i] >= maxLimits[i])) {
                revert InvalidInput();
            }
            releaseLimits[tokens[i]] = IVault.ReleaseLimit(minLimits[i], maxLimits[i]);
        }
    }

    /// @notice Upgrades the implementation behind the proxy to `newImplementation`
    function upgradeImplementation(
        address newImplementation
    ) external onlyAdmin returns (address) {
        if (newImplementation == address(0)) revert InvalidInput();
        ERC1967Utils.upgradeToAndCall(newImplementation, "");
        return newImplementation;
    }

}
